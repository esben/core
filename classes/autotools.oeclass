# -*- mode:python; -*-

# Autoconf sitefile handling
inherit c
inherit siteinfo
inherit libtool
inherit make

# create new classes:
#   autoconf
#   autoreconf
#   automake ?
# and deprecate the following classed (for removal in core 4):
#   autotools
#   autotools-autoreconf
# and recipes should thus inherit explicitly from either of
#   autotools
#   autoreconf
#   automake
# and optionally also from one or more of
#   libtool
# the now deprecated autotools class should
#   inherit autoconf libtool
#   and provide autotools_* legacy shell function wrappers
# the now deprecated autotools-autoreconf should
#   inherit autoreconf

EXTRA_OECONF[emit] = "do_configure"
EXTRA_OECONF:>machine		= " cross_compiling=yes"
EXTRA_OECONF:>cross		= " cross_compiling=yes"
EXTRA_OECONF:>sdk-cross		= " cross_compiling=yes"
EXTRA_OECONF:>sdk		= " cross_compiling=yes"
EXTRA_OECONF:>canadian-cross	= " cross_compiling=yes"

#EXTRA_OECONF += "--with-libtool=`which ${LIBTOOL}`"

# Arch tuple arguments for configure
OECONF_ARCHTUPLE = "--build=${BUILD_ARCH} --host=${HOST_ARCH}"
OECONF_ARCHTUPLE:>cross			= " --target=${TARGET_ARCH}"
OECONF_ARCHTUPLE:>sdk-cross 		= " --target=${TARGET_ARCH}"
OECONF_ARCHTUPLE:>canadian-cross	= " --target=${TARGET_ARCH}"
OECONF_ARCHTUPLE[emit] = "do_configure"

CONFIGURE_SCRIPT ?= "${S}/configure"

autotools_configure () {
    if [ -x "${CONFIGURE_SCRIPT}" ] ; then
        ${CONFIGURE_SCRIPT} \
 ${OECONF_ARCHTUPLE}\
 --prefix=${prefix} --exec_prefix=${exec_prefix}\
 --bindir=${bindir} --sbindir=${sbindir}\
 --libexecdir=${libexecdir} --datadir=${datadir} --sysconfdir=${sysconfdir}\
 --sharedstatedir=${sharedstatedir} --localstatedir=${localstatedir}\
 --libdir=${libdir} --includedir=${includedir}\
 --infodir=${infodir} --mandir=${mandir}\
 ${EXTRA_OECONF} $@
    else
        oefatal "no configure script found"
    fi
}
autotools_configure[emit] = "do_configure"

do_configure () {
    do_configure_autotools
}
do_configure_autotools () {
    if [ -f Makefile -a "${AUTOTOOLS_DISTCLEAN}" != "0" ] ; then
        ${MAKE} distclean || true
    fi

    if [ -e "${CONFIGURE_SCRIPT}" ]; then
        autotools_configure
    else
        oenote "nothing to configure"
    fi

    libtool_script_fixup
}
AUTOTOOLS_DISTCLEAN ?= "1"
