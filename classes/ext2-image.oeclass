## Used for creating ext2 images.
##
## Creates an ext2 image.
##
## @var IMAGE_BASENAME Set to a different basename.

IMAGE_BASENAME ?= "${TARGET_MACHINE_ID}-${PN}"

inherit image

EXT2_IMAGE_DEPENDS = "native:genext2fs"
CLASS_DEPENDS += "${EXT2_IMAGE_DEPENDS}"

IMAGE_CREATE_FUNCS += "genext2fs_image"

# Block size is hardcoded in genext2fs to 1024 bytes
EXT2_IMAGE_SIZE ?= "20480"
EXT2_IMAGE_OPTIONS ?= "-z -q -f -b ${EXT2_IMAGE_SIZE}"

genext2fs_image () {
	genext2fs ${EXT2_IMAGE_OPTIONS} \
		 -d ${IMAGE_DIR} \
		 ${B}/${IMAGE_BASENAME}.ext2
}

do_install[postfuncs] += "do_install_ext2_image"
do_install_ext2_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}.ext2 ${D}/
}

FILES_${PN} += "/*.ext2"

do_deploy[postfuncs] += "do_deploy_ext2_image"
do_deploy_ext2_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}.ext2 \
		${IMAGE_DEPLOY_DIR}/${IMAGE_FULLNAME}.ext2
	ln -sf ${IMAGE_FULLNAME}.ext2 \
		${IMAGE_DEPLOY_DIR}/${IMAGE_BASENAME}.ext2
}

# Local Variables:
# mode: python
# End:
