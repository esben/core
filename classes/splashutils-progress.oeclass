## Class giving support for splash screen support with progress information
##
## Splashutils may shown progress during boot. The progress information is
## attached on initscripts. All initscripts contains the "splash_utils
## PLACEHOLDER" line which may be replaced by a given integer.  When the
## initscript is installed, if the splashutils_progress_information USE flag
## is set, the line will be replaced by "splash_utils <integer>" or removed if
## the USE flag is not set.
##
## @useflag splashutils_progress_information Enable splash utils progress
##          information. Is disabled as default.

CLASS_FLAGS += "splashutils_progress_information"

SPLASHUTILS_INITSCRIPTS ?= ""

do_install[postfuncs] += "do_install_fix_splashutils"

python do_install_fix_splashutils () {
    import os, fileinput
    path = d.get("D")
    os.chdir(path)
    sysconfdir=d.get("sysconfdir")
    initscripts = ((d.get("SPLASHUTILS_INITSCRIPTS"))).split()
    for script in initscripts:
        msg=d.get("USE_" + script + "_splashutils_msg")
        script_file = script.replace("_", "-")
        for line in fileinput.FileInput("." + sysconfdir + "/init.d/" + script_file, inplace=1):
            if d.get("USE_splashutils_progress_information") and d.get("USE_" + script + "_splashutils_progress"):
                progress=d.get("USE_" + script + "_splashutils_progress")
                line=line.replace("splash_progress PLACEHOLDER","test -x /sbin/splash-functions.sh && . /sbin/splash-functions.sh && splash_progress " + progress + " " + msg)
            else:
                line=line.replace("splash_progress PLACEHOLDER","")
            print line,
}

# Local Variables:
# mode: python
# End:
