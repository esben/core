## OE-lite class for recipes providing content for mdev.conf file
##
## If a recipe has content for /etc/mdev.conf, ensure MDEV_CONF_FILES
## points at one or more files containing mdev configurations. Moves the
## mdev configurations to a recipe specific folder, so that multiple mdev
## configuration files from one or more recipes may be combined later. The
## combining of mdev configurations is done in the image_mdev class.
##
## @useflag mdev Enable or disable mdev. Default disabled.
##
## @var MDEV_CONF_FILES A list of mdev configuration files. Set to
##      ${SRCDIR}/mdev.conf by default.

require conf/mdev.conf

CLASS_FLAGS += "busybox_mdev"

RDEPENDS_${PN}:>USE_busybox_mdev = " busybox-mdev"

MDEV_CONF_FILES ?= "${SRCDIR}/mdev.conf"

MDEV_POSTFUNCS:USE_busybox_mdev = "do_install_mdev"
do_install[postfuncs] += "${MDEV_POSTFUNCS}"
do_install_mdev () {
	i=0
	for f in ${MDEV_CONF_FILES} ; do
		# only create mdevdir when needed, and let it fail silently when
		# called more than once
		mkdir -p ${D}/${mdevdir}
		let i=$i+1
		cp $f ${D}/${mdevdir}/${PN}.$i
	done
}
do_install_mdev[expand] = "3"

# Local Variables:
# mode: python
# End:
