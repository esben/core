# -*- mode:python; -*-

require conf/make.conf

export MAKE

oe_runmake() {
	${MAKE} $PARALLEL_MAKE ${EXTRA_OEMAKE} "$@"
}

export PARALLEL_MAKE
PARALLEL_MAKE[nohash] = True

# Pass parallel make options to the compile task only
# FIXME: verify after implementing onlytask varflag
PARALLEL_MAKE[onlytask] = "compile"

do_compile() {
	do_compile_make
}
do_compile_make() {
	if [ -e Makefile -o -e makefile ]; then
		oe_runmake ${EXTRA_OEMAKE_COMPILE} || die "make failed"
	else
		oenote "nothing to compile"
	fi
}
EXTRA_OEMAKE_COMPILE[emit] = "do_compile"

do_install () {
	do_install_make
}
do_install_make () {
	${MAKE} ${EXTRA_OEMAKE} ${EXTRA_OEMAKE_INSTALL} ${MAKE_DESTDIR} install
}
EXTRA_OEMAKE_INSTALL[emit] = "do_install"
