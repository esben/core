# -*- mode:python; -*-
#
# inherit this for (single) library recipes
#

DEPENDS_${PN}		= "${PN}-dev_${PV}"
DEPENDS_${PN}-dev	= ""

FILES_${PN}		= "${base_libdir}/lib*${SOLIBS} ${libdir}/lib*${SOLIBS}"
ALLOW_EMPTY_${PN}-dev	= "0"

addhook library_depends to post_recipe_parse after fixup_package_type before blacklist

def library_depends(d):
    pkg = d.get("PN")
    pkg_type = d.get("PACKAGE_TYPE_" + pkg) or d.get("RECIPE_TYPE")
    pv = d.get("PV")
    devpkg = pkg + "-dev"
    pkg_depends = (d.get("DEPENDS_" + pkg) or "").split()

    if not pkg.startswith("lib"):
        library_name = d.get("LIBRARY_NAME")
        if not library_name:
            library_name = "lib" + pkg
        provides = (d.get("PROVIDES_" + pkg) or "").split()
        if not library_name in provides:
            provides.append(library_name)
            d.set("PROVIDES_" + pkg, " ".join(provides))
        provides = (d.get("PROVIDES_" + devpkg) or "").split()
        if not library_name + "-dev" in provides:
            provides.append(library_name + "-dev")
            d.set("PROVIDES_" + devpkg, " ".join(provides))

    if not pkg_type in ("machine", "sdk", "canadian-cross"):
        return

    pkg_rdepends = d.get("RDEPENDS_" + pkg)
    if pkg_rdepends is None:
        pkg_rdepends = []
        for dep in pkg_depends:
            item = oelite.item.OEliteItem(dep, ("DEPENDS", pkg_type))
            if item.type != pkg_type:
                continue
            if dep == (devpkg + "_" + pv):
                continue
            if item.name.startswith("lib"):
                pkg_rdepends.append(dep)
        if pkg_rdepends:
            d.set("RDEPENDS_" + pkg, " ".join(pkg_rdepends))
    else:
        pkg_rdepends = pkg_rdepends.split()

    devpkg_rdepends = (d.get("RDEPENDS_" + devpkg) or "").split()
    for dep in pkg_rdepends:
        item = oelite.item.OEliteItem(dep, ("RDEPENDS", pkg_type))
        if item.name.startswith("lib") and not item.name.endswith("-dev"):
            if item.version:
                devdep = "%s:%s_%s"%(item.type, item.name + "-dev", item.type)
            else:
                devdep = "%s:%s"%(item.type, item.name + "-dev")
            devpkg_rdepends.append(dep + "-dev")
    d.set("RDEPENDS_" + devpkg, " ".join(devpkg_rdepends))
