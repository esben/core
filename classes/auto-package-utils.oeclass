## Multiutils split functionality class
##
## If having a package that contains multiple utils, then this class may be
## utilized to ensure correct split of the utilities. Set the variable
## AUTO_PACKAGE_UTILS to a list of utilities, and a number
## of extra packages equal to the number of items in the list will be created.
## E.g. if having a package contains /usr/bin/foo and /usr/bin/bar,
## the list would be AUTO_PACKAGE_UTILS = "foo bar". This will create 2
## additional package named ${PN}-foo and ${PN}-bar that provides util/foo and
## util/bar.
##
## @var AUTO_PACKAGE_UTILS Set to a list of libraries
## @var AUTO_PACKAGE_UTILS_RDEPENDS Set to a common RDEPEND for all libraries in
##      the list
## @var AUTO_PACKAGE_UTILS_DEPENDS Set to a common DEPEND for all libraries in
##      the list
## @var AUTO_PACKAGE_UTILS_BASENAME Base package name for produced packages.
##      Defaults to PN.

PACKAGES =+ "${AUTO_PACKAGE_UTILS_PACKAGES}"

AUTO_PACKAGE_UTILS ?= ""

META_EMIT_PREFIX += "package:AUTO_PACKAGE_UTILS split:AUTO_PACKAGE_UTILS"

addhook auto_package_utils to post_recipe_parse before base_detect_machine_override fixup_package_type before blacklist

AUTO_PACKAGE_UTILS_BASENAME ?= "${PN}"

def auto_package_utils (d):
    import warnings

    basename = d.get("AUTO_PACKAGE_UTILS_BASENAME")
    utils = (d.get("AUTO_PACKAGE_UTILS") or "").split()
    auto_rdepends = d.get("AUTO_PACKAGE_UTILS_RDEPENDS")
    auto_depends = d.get("AUTO_PACKAGE_UTILS_DEPENDS")
    exeext = d.get("HOST_EXEEXT") or ""
    packages = []
    doc_packages = []
    provides = []

    def get_extra_files(pkg):
        #return (d.get("FILES_" + pkg) or "").split()
        files = d.get("FILES_" + pkg)
        if files is None:
            files = d.get("EXTRA_FILES_" + pkg)
            if files:
                warnings.warn(
                    "EXTRA_FILES_* is deprecated, use FILES_* instead")
        return (files or "").split()

    for util in utils:
        if ":" in util:
            utilname, util = util.split(":", 1)
            utilname = utilname.replace("_", "-").replace(".", "-").lower()
        else:
            utilname = util.replace("_", "-").replace(".", "-").lower()
        pkg = "%s-%s"%(basename, utilname)
        utilname = "util/" + utilname
        docpkg = pkg + "-doc"
        packages += [ pkg ]
        doc_packages += [ docpkg ]
        provides += [ utilname ]

        d.set("FILES_" + pkg,
              "${base_sbindir}/%s%s "%(util, exeext) +
              "${base_bindir}/%s%s "%(util, exeext) +
              "${sbindir}/%s%s "%(util, exeext) +
              "${bindir}/%s%s "%(util, exeext) +
              "${libexecdir}/%s%s "%(util, exeext) +
              " ".join(get_extra_files(pkg)))

        d.set("FILES_" + docpkg,
              "${mandir}/man?/%s.* "%(util) +
              " ".join(get_extra_files(docpkg)))

        if auto_rdepends:
            rdepends = d.get("RDEPENDS_" + pkg)
            if rdepends:
                d.set("RDEPENDS_" + pkg, rdepends + " " + auto_rdepends)
            else:
                d.set("RDEPENDS_" + pkg, auto_rdepends)

        if auto_depends:
            depends = d.get("DEPENDS_" + pkg)
            if depends:
                d.set("DEPENDS_" + pkg, depends + " " + auto_depends)
            else:
                d.set("DEPENDS_" + pkg, auto_depends)

        pkg_provides = (d.get("PROVIDES_" + pkg) or "").split()
        pkg_provides.append(utilname)
        d.set("PROVIDES_" + pkg, " ".join(pkg_provides))
    
    d.set("AUTO_PACKAGE_UTILS_PACKAGES", " ".join(packages))
    d.set("AUTO_PACKAGE_UTILS_DOC_PACKAGES", " ".join(doc_packages))
    d.set("AUTO_PACKAGE_UTILS_PROVIDES", " ".join(provides))

# Local Variables:
# mode: python
# End:
