## Class for handling creation of ubifs images
##
## Handles creation of ubifs images. Set the three USE flags:
## ubifs_image_leb_size, ubifs_image_max_leb_cnt, and ubifs_image_min_io_size
## to the values according to your device.
##
## @useflag ubifs_image_leb_size Set it to the Logical eraseblock size of your
##          device.
## @useflag ubifs_image_max_leb_cnt Set the maximum leb count according to
##          your device.
## @useflag ubifs_image_min_io_size Minimum amount of data which may be
##           written to the device.
##
## @var UBIFS_IMAGE_COMPR_TYPE Set the compression type of the ubifs. Default
##      set to lzo.
## @var UBIFS_IMAGE_OPTIONS Set the ubifs image options for creation. Default
##      sets the chosen leb-size, max-leb-cnt and min-io-size which are set by
##      the three USE flags ubifs_image_leb_size, ubifs_image_max_leb_cnt, and
##      ubifs_image_min_io_size.

IMAGE_BASENAME ?= "${TARGET_MACHINE_ID}-${PN}"

inherit image

UBIFS_IMAGE_DEPENDS = "native:mtd-utils-mkfs-ubifs"
CLASS_DEPENDS += "${UBIFS_IMAGE_DEPENDS}"

IMAGE_CREATE_FUNCS += "ubifs_image"

CLASS_FLAGS += " \
	ubifs_image_leb_size \
	ubifs_image_max_leb_cnt \
	ubifs_image_min_io_size"

UBIFS_IMAGE_LEB_SIZE:USE_ubifs_image_leb_size = "${USE_ubifs_image_leb_size}"
UBIFS_IMAGE_MAX_LEB_CNT:USE_ubifs_image_max_leb_cnt = "${USE_ubifs_image_max_leb_cnt}"
UBIFS_IMAGE_MIN_IO_SIZE:USE_ubifs_image_min_io_size = "${USE_ubifs_image_min_io_size}"

UBIFS_IMAGE_OPTIONS += "--leb-size=${UBIFS_IMAGE_LEB_SIZE}"
UBIFS_IMAGE_OPTIONS += "--max-leb-cnt=${UBIFS_IMAGE_MAX_LEB_CNT}"
UBIFS_IMAGE_OPTIONS += "--min-io-size=${UBIFS_IMAGE_MIN_IO_SIZE}"

UBIFS_IMAGE_COMPR_TYPE ?= "lzo"
UBIFS_IMAGE_OPTIONS += "--compr=${UBIFS_IMAGE_COMPR_TYPE}"

ubifs_image () {
    mkfs.ubifs ${UBIFS_IMAGE_OPTIONS} \
        --root=${IMAGE_DIR} \
        --output=${B}/${IMAGE_BASENAME}.ubifs
}

do_install[postfuncs] += "do_install_ubifs_image"
do_install_ubifs_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}.ubifs ${D}/
}

FILES_${PN} += "/*.ubifs"

do_deploy[postfuncs] += "do_deploy_ubifs_image"
do_deploy_ubifs_image () {
	install -m 664 ${B}/${IMAGE_BASENAME}.ubifs \
		${IMAGE_DEPLOY_DIR}/${IMAGE_FULLNAME}.ubifs
	ln -sf ${IMAGE_FULLNAME}.ubifs \
		${IMAGE_DEPLOY_DIR}/${IMAGE_BASENAME}.ubifs
}

# Local Variables:
# mode: python
# End:
