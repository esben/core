## Class for building u-boot images.
##
## Allows building u-boot bootloader with a variety of options.
##
## @useflag uboot_extra_env Set this to extra env in u-boot. Will be appended
##          to the existing default environment. Is empty by default.
## @useflag uboot_config Name of the uboot config to use. Default set to
##          ${MACHINE}_config.
## @useflag uboot_config_file Filename of the uboot config to use.
## @useflag uboot_maxsize Enables checking of sizecheck. Default disabled.
##          Set this to the maximum size allowed of your u-boot image.
## @useflag uboot_srec Enable compilation of srec. Default disabled.
##
## @var UBOOT_IMAGE Name of the image to build. Is u-boot.bin by default.
## @var UBOOT_IMAGE_BASE Basename of the deployed image. U-boot by default.
## @var UBOOT_IMAGE_FILENAME Filename of the compiled uboot image.
## @var UBOOT_IMAGE_DEPLOY_NAME Set the deployed name of the compiled u-boot
##      bootloader. Is ${PN} by default.
## @var UBOOT_IMAGE_DEPLOY_EXT Set the extention to the deployed compiled
##      u-boot bootloader. Is .bin by default.
## @var UBOOT_IMAGE_DEPLOY_FILE Full filename of the deployed compiled u-boot
##      bootloader.
## @var UBOOT_IMAGE_DEPLOY_LINK Full filename of the link deployed, pointing
##      at UBOOT_IMAGE_DEPLOY_FILE.

DESCRIPTION ?= "Das U-Boot -- the Universal Boot Loader"
HOMEPAGE ?= "http://www.denx.de/wiki/U-Boot"
LICENSE ?= "GPL"

require conf/u-boot.conf

RECIPE_TYPES = "machine"

inherit c make kernel-arch

CLASS_DEPENDS += "native:u-boot-tools-mkimage"

# Why bother?  U-Boot will most likely stay broken for parallel builds
PARALLEL_MAKE = ""

EXTRA_OEMAKE = "${EXTRA_OEMAKE_ARCH} CROSS_COMPILE=${TARGET_PREFIX}"
EXTRA_OEMAKE_ARCH ?= "ARCH=${KERNEL_ARCH}"

CFLAGS[unexport]   = "1"
CPPFLAGS[unexport] = "1"
LDFLAGS[unexport]  = "1"

do_configure[prefuncs] += "${DO_CONFIGURE_EXTRAENV}"
DO_CONFIGURE_EXTRAENV = ""
DO_CONFIGURE_EXTRAENV:USE_uboot_extra_env = "do_configure_extraenv"
def do_configure_extraenv(d):
    from oebakery import debug, info, warn, err, die
    config_filename = os.path.join("include/configs",
                                   d.get("USE_uboot_config_file"))
    extra_env_lines = []
    for line in (d.get("USE_uboot_extra_env").splitlines()):
        line = line.strip()
        if not line:
            continue
        extra_env_lines.append('\t"%s\\0"'%(line,))
    try:
        with open(config_filename) as config_file:
            config = config_file.read()
    except:
        err("Failed to open file %s"%(config_filename))
        return False
    # Search to the CONFIG_EXTRA_ENV_SETTINGS define
    define = re.search(r"^#define[ \t]+CONFIG_EXTRA_ENV_SETTINGS[ \t]+",
                       config, re.MULTILINE)
    if not define:
        print "ERROR: cannot find CONFIG_EXTRA_ENV_SETTINGS in %s"%(
            config_filename)
        return False
    # Get the current define value
    current = re.match(r"([^\n]*\\\n)*([^\n]*\n)",
                       config[define.end():])
    last_line = config[define.end()+current.start(2):
                           define.end()+current.end(2)].strip()
    # Write out config file with USE_uboot_extra_env inserted
    with open(config_filename, "w") as config_file:
        config_file.write(config[:define.end() + current.end(1)])
        if last_line:
            config_file.write(config[define.end() + current.start(2):
                                         define.end() + current.end(2) - 1] +
                              " \\\n")
        config_file.write(" \\\n".join(extra_env_lines) + "\n")
        if last_line:
            config_file.write(config[define.end() + current.end(2):])
        else:
            config_file.write(config[define.end() + current.start(2):])
    return True

do_configure () {
    oe_runmake ${USE_uboot_config}
}

do_compile () {
    oe_runmake ${UBOOT_IMAGE}
}

# Support checking the u-boot image size
inherit sizecheck
UBOOT_SIZECHECK = ""
UBOOT_SIZECHECK:USE_uboot_maxsize = "${UBOOT_IMAGE}:${USE_uboot_maxsize}"
SIZECHECK += "${UBOOT_SIZECHECK}"

do_install () {
    install -d ${D}${bootdir}
    install -m 0644 ${UBOOT_IMAGE} ${D}${bootdir}
    install -m 0644 ${UBOOT_IMAGE_BASE} ${D}${bootdir}
}

PACKAGES = "${PN} ${PN}-elf"
FILES_${PN} = "${bootdir}/${UBOOT_IMAGE_FILENAME}"
FILES_${PN}-elf = "${bootdir}/${UBOOT_IMAGE_BASE}"

PROVIDES_${PN} = "u-boot"
PROVIDES_${PN}-elf = "u-boot-elf"

addtask deploy before build after compile
do_deploy[dirs] = "${IMAGE_DEPLOY_DIR} ${S}"
REBUILD ?= "1"

do_deploy () {
    install -m 0644 ${UBOOT_IMAGE} \
	${IMAGE_DEPLOY_DIR}/${UBOOT_IMAGE_DEPLOY_FILE}
    md5sum <${UBOOT_IMAGE} \
	>${IMAGE_DEPLOY_DIR}/${UBOOT_IMAGE_DEPLOY_FILE}.md5

    cd ${IMAGE_DEPLOY_DIR}
    if [ -n "${UBOOT_IMAGE_DEPLOY_LINK}" ] ; then
	for ext in "" ".md5"; do
	    rm -f  ${UBOOT_IMAGE_DEPLOY_LINK}$ext
	    ln -sf ${UBOOT_IMAGE_DEPLOY_FILE}$ext \
		   ${UBOOT_IMAGE_DEPLOY_LINK}$ext
	done
    fi
}

CLASS_FLAGS += "uboot_srec"

UBOOT_SREC_IMAGE = "${UBOOT_IMAGE_BASE}.s19"
UBOOT_SREC_IMAGE_DEPLOY_EXT ?= ".s19"
UBOOT_SREC_IMAGE_DEPLOY_FILE ?= "${MACHINE}-${UBOOT_IMAGE_DEPLOY_NAME}-${PV}-${DATETIME}${UBOOT_SREC_IMAGE_DEPLOY_EXT}"
UBOOT_SREC_IMAGE_DEPLOY_LINK ?= "${MACHINE}-${UBOOT_IMAGE_DEPLOY_NAME}${UBOOT_SREC_IMAGE_DEPLOY_EXT}"

do_compile[postfuncs] += "${DO_COMPILE_SREC}"
DO_COMPILE_SREC:USE_uboot_srec = "do_compile_srec"
do_compile_srec () {
	srec_cat ${UBOOT_IMAGE} -Binary -o ${UBOOT_SREC_IMAGE} -Motorola
}

do_install[postfuncs] += "${DO_INSTALL_SREC}"
DO_INSTALL_SREC:USE_uboot_srec = "do_install_srec"
do_install_srec () {
	install -m 0644 ${UBOOT_SREC_IMAGE} ${D}${bootdir}
}

PACKAGES += "${PN}-srec"
FILES_${PN}-srec = "${bootdir}/${UBOOT_SREC_IMAGE}"
PROVIDES_${PN}-srec = "u-boot-srec"

do_deploy[postfuncs] += "${DO_DEPLOY_SREC}"
DO_DEPLOY_SREC:USE_uboot_srec = "do_deploy_srec"
do_deploy_srec () {
	install -m 0644 ${UBOOT_SREC_IMAGE} \
		${IMAGE_DEPLOY_DIR}/${UBOOT_SREC_IMAGE_DEPLOY_FILE}
	md5sum <${UBOOT_IMAGE} \
		>${IMAGE_DEPLOY_DIR}/${UBOOT_SREC_IMAGE_DEPLOY_FILE}.md5

	cd ${IMAGE_DEPLOY_DIR}
	if [ -n "${UBOOT_SREC_IMAGE_DEPLOY_LINK}" ] ; then
		for ext in "" ".md5"; do
			rm -f  ${UBOOT_SREC_IMAGE_DEPLOY_LINK}$ext
			ln -sf ${UBOOT_SREC_IMAGE_DEPLOY_FILE}$ext \
			       ${UBOOT_SREC_IMAGE_DEPLOY_LINK}$ext
		done
	fi
}

# Local Variables:
# mode: python
# End:
